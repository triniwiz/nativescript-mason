plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
}

android {
  namespace 'org.nativescript.mason.masonkit'
  compileSdk 35
  defaultConfig {
    minSdk 21
    targetSdk 35
    multiDexEnabled true
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }
  kotlinOptions {
    jvmTarget = '17'
  }
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

cargo {
  module = "../../../../../"
  libname = "masonnative"

//  targets = ["arm64"]
 // targets = ["x86"]
  apiLevels = [
    "arm": 21,
    "arm64": 21,
    "x86": 21,
    "x86_64": 21
  ]
  targets = ["arm", "arm64", "x86", "x86_64"]
//  profile = "release"
  targetDirectory = "../../../../../target"
  verbose = true
  extraCargoBuildArguments = ['-p', 'mason-android']
}

tasks.configureEach { task ->
  if ((task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease')) {
    task.dependsOn 'cargoBuild'
  }
}

dependencies {

  implementation 'androidx.core:core-ktx:1.9.0'
  implementation 'androidx.appcompat:appcompat:1.5.1'
  implementation 'com.google.code.gson:gson:2.10.1'
  testImplementation 'junit:junit:4.13.2'
  androidTestImplementation 'androidx.test.ext:junit:1.1.4'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
  implementation 'androidx.multidex:multidex:2.0.1'

}

