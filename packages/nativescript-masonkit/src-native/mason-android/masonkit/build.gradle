plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
}


def detectHostArch = {
  def arch = System.getProperty("os.arch").toLowerCase(Locale.ROOT)
  if (arch.contains("aarch64") || arch.contains("arm64")) return "arm64"
  if (arch.contains("x86_64") || arch.contains("amd64")) return "x86_64"
  if (arch.contains("x86")) return "x86"
  if (arch.contains("arm")) return "arm"
  throw new GradleException("Unsupported host architecture: $arch")
}

def rustTargets = {
  def prop = project.findProperty("rust.targets")
  if (!prop || prop == "host") {
    return [detectHostArch()]
  }
  if (prop == "all") {
    return ["arm", "arm64", "x86", "x86_64"]
  }
  return prop.split(",") as List<String>
}()
def isRelease = gradle.startParameter.taskNames.any {
  it.toLowerCase().contains("release")
}

def rustProfile = isRelease ? "release" : null

android {
  namespace 'org.nativescript.mason.masonkit'
  compileSdk 36
  defaultConfig {
    minSdk 21
    targetSdk 36
    multiDexEnabled true
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
    // using for blur pre RenderNode
    renderscriptTargetApi 28
    renderscriptSupportModeEnabled true
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }
  kotlinOptions {
    jvmTarget = '17'
  }
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

cargo {
  module = "../../../../../"
  libname = "masonnative"

  apiLevels = [
    "arm"   : 21,
    "arm64" : 21,
    "x86"   : 21,
    "x86_64": 21
  ]

  if (rustProfile == "release") {
    profile = "release"     // gradle plugin will pass --release
  }


  targets = rustTargets
  targetDirectory = "../../../../../target"
  verbose = true
  extraCargoBuildArguments = ['-p', 'mason-android']

  exec = { spec, toolchain ->
    spec.environment("RUSTFLAGS", "-C link-arg=-Wl,--hash-style=sysv -C link-arg=-Wl,-z,max-page-size=16384")
  }
}

tasks.configureEach { task ->
  if ((task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease')) {
    task.dependsOn 'cargoBuild'
  }

  if (name == "mergeDebugJniLibFolders" || name == "mergeReleaseJniLibFolders") {
    outputs.upToDateWhen { false }
    dependsOn("cargoBuild")
  }
}

//afterEvaluate {
//    tasks.withType(CargoBuildTask).configureEach { buildTask ->
//        tasks.withType(MergeSourceSetFolders).configureEach {
//            inputs.dir(layout.buildDirectory.dir("rustJniLibs${File.separator}${buildTask.toolchain.folder}"))
//            dependsOn(buildTask)
//        }
//    }
//}


dependencies {
  implementation 'androidx.core:core-ktx:1.12.0'
  implementation 'androidx.appcompat:appcompat:1.6.1'
  implementation 'androidx.activity:activity:1.10.1'
  implementation 'com.google.code.gson:gson:2.13.2'
  testImplementation 'junit:junit:4.13.2'
  androidTestImplementation 'androidx.test.ext:junit:1.2.1'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
  implementation 'com.github.bumptech.glide:glide:4.16.0'
  annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
}

